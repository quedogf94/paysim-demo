plugins {
    id 'java'
    id 'application'
}

group 'io.sisu'
version '0.6.0-SNAPSHOT'
description 'Using PaySim, populates a graph in a target Neo4j graph database'

sourceCompatibility = 1.8
targetCompatibility = 1.8

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        maven { url 'https://jitpack.io' }
    }
}

dependencies {
    implementation 'org.paysim:paysim:2.5.0-SNAPSHOT'
    implementation 'org.neo4j.driver:neo4j-java-driver:4.1.1'
    implementation 'net.sourceforge.argparse4j:argparse4j:0.8.1'
    implementation 'org.slf4j:slf4j-simple:1.7.30'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.2'
    testRuntimeOnly 'org.neo4j:neo4j-common:4.1.3'
    testRuntimeOnly 'org.neo4j:neo4j-io:4.1.3'

    testImplementation 'org.neo4j.gds:core:1.3.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.2'
}

application {
    mainClassName = 'io.sisu.paysim.App'
}

task packagePaySimProperties(type: Copy) {
    from '.'
    include 'PaySim.properties'
    include 'paramFiles/**'
    into "$buildDir/paysimprops"
}
distributions {
    main {
        contents {
            from "$buildDir/paysimprops"
        }
    }
}

distZip.dependsOn(packagePaySimProperties)
distTar.dependsOn(packagePaySimProperties)

test {
    useJUnitPlatform()
}
